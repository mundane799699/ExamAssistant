apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def keyStore = file('examassistant.jks')

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
//        manifestPlaceholders = [UMENG_CHANNEL: "wandoujia"] //默认渠道
    }

    signingConfigs {
        config {
            keyAlias 'mundane_key'
            keyPassword '799699'
            storePassword '799699'
            storeFile file('examassistant.jks')
        }
    }
    //自动多渠道打包
    productFlavors {
        yingyongbao {}
        kuan {}
        fir {}
        googleplay {}
        //...添加其它渠道
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL: name]
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true // 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keyStore.exists()) {
                println "examassistant: using mundane's key"
                signingConfig signingConfigs.config
            } else {
                println "examassistant: using default key"
            }

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "Examassistant_release_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        def outputPath = "${rootDir}/apk"
                        output.outputFile = new File(outputPath, fileName)
                    }
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

//  greendao配置
greendao {
    schemaVersion 1 //版本号，升级时可配置
    //    daoPackage 'com.mundane.examassistant.db.dao'     //生成的dao文件所在的目录
    //    targetGenDir 'src/main/java'                   //生成目录
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':banner')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha9'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'org.greenrobot:greendao:3.2.2'
    compile 'com.alibaba:fastjson:1.1.56.android'
    compile 'com.android.support:design:25.3.1'
    compile 'com.github.bumptech.glide:glide:4.0.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
}
